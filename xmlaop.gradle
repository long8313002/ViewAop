apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'

def siteUrl = 'https://github.com/smallSohoSolo/McImage'
def gitUrl = 'https://github.com/smallSohoSolo/McImage.git'
group = "com.smallsoho.mobcase"
version = "1.5.1"

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

install {
    repositories.mavenInstaller {
        pom {
            project {

                packaging 'jar'
                name 'McImage'
                url siteUrl

                licenses {
                    license {
                        name 'Apache-2.0'
                        url 'https://opensource.org/licenses/Apache-2.0'
                    }
                }

                developers {
                    developer {
                        id properties.getProperty("JCENTER_ID")
                        name properties.getProperty("JCENTER_NAME")
                        email properties.getProperty("JCENTER_EMAIL")
                    }
                }

                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = properties.getProperty("bintrayUser")
    key = properties.getProperty("bintrayApikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "McImage"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
